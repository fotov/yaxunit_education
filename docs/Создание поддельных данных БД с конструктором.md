---
share: true
---

https://bia-technologies.github.io/yaxunit/docs/user-api/test-data/#генерация-фейкового-документа
# Пример использования
```bsl
#Если Сервер Тогда
	Конструктор = ЮТест.Данные().КонструкторОбъекта(Документы.ПоступлениеТоваров);
#Иначе
	Конструктор = ЮТест.Данные().КонструкторОбъекта("Документы.ПоступлениеТоваров");
#КонецЕсли

Конструктор
	.ФикцияОбязательныхПолей()
	.Фикция("Склад")
	.Установить("Контрагент", ЮТест.Данные().СоздатьЭлемент("Справочники.Контрагенты"))
	.ТабличнаяЧасть("Товары");

Для Инд1 = 1 По ЮТест.Данные().СлучайноеЧисло(1, 5) Цикл
	Конструктор.ДобавитьСтроку()
		.Фикция("Номенклатура")
		.Установить("Количество", ЮТест.Данные().СлучайноеПоложительноеЧисло(20))
		.Установить("Цена", ЮТест.Данные().СлучайноеПоложительноеЧисло(9999, 2))
		.Установить("Сумма", Конструктор.ДанныеСтроки().Цена * Конструктор.ДанныеСтроки().Количество);
КонецЦикла;

Ссылка = Конструктор.Провести();
```
# Методы ЮТест.Данные()
- `КонструкторОбъекта` - Возвращает конструктор создания тестовых данных
> [!Info] Конструктор имеет ряд особенностей:
>* Нельзя использовать параллельно несколько конструкторов.
>Например:
>```bsl
>Пользователь = КонструкторОбъекта(Справочники.Пользователи);
>Документ = КонструкторОбъекта(Документы.Приход);
>...
>Пользователь.Записать();
>Документ.Провести();
>```
>* Создание объекта происходит при вызове методов `Записать` и `Провести`, а создание реквизитов происходит во время вызова методов установки.
>* При использовании на клиенте все значения должны быть сериализуемыми.
# Методы Конструктора
> [!Warning] Официальная страница документации не содержит описания методов. Методы можно посмотреть в Программном интерфейсе модуля объекта обработки ЮТКонструкторТестовыхДанных
- `Установить` - Устанавливает значение реквизита создаваемого объекта. Если метод вызывается после вызова см. ТабличнаяЧасть, то устанавливается значение реквизита строки табличной части 
- `Фикция` - Устанавливает фиктивное значение реквизита создаваемого объекта. Если метод вызывается после вызова см. ТабличнаяЧасть, то устанавливается значение реквизита строки табличной части. 
> [!info] На основании менеджера и имени реквизита вычисляется его тип, а по типу генерируется фиктивное значение.
> * Для примитивных значение генерируется случайное значение
> * Для ссылочных типов создается новый объект
> * Для перечислений (в том числе системных) выбирается случайно значение перечисления
- `ФикцияОбязательныхПолей` - Заполняет обязательные поля объекта фиктивными значениями. Если метод вызывается после вызова см. ТабличнаяЧасть, то устанавливаются значения реквизитов строки табличной части.
> [!info] На основании метаданных определяются обязательные поля (Проверка заполнения = Выдавать ошибку), для них генерируются и устанавливаются фиктивные значение.
> * Для примитивных значение генерируется случайное значение
> * Для ссылочных типов создается новый объект
> * Для перечислений (в том числе системных) выбирается случайно значение перечисления
- `ТабличнаяЧасть` - Переключает конструктор на заполнение табличной части объекта.
- `ДобавитьСтроку` - Добавляет новую строку табличной части объекта.
- `УстановитьДополнительноеСвойство` - Устанавливает дополнительное свойство объекта.
- `Записать` - Создает новый объект и возвращает его или ссылку. 
> [!Warning] Важно, после создания данные не очищаются, поэтому многократный вызов метода создаст множество одинаковых объектов. Если вы хотите создать полностью новый объект, необходимо по-новой инициировать конструктор.
- `НовыйОбъект` - Создает и возвращает новый объект (не записывая его).
> [!Warning] Важно, после создания данные не очищаются, поэтому многократный вызов метода создаст множество одинаковых объектов. Если вы хотите создать полностью новый объект, необходимо по-новой инициировать конструктор.
- `Провести` - Создает и проводит новый документ. Возвращает объект или или ссылку на него.
> [!Warning] Важно, после создания данные не очищаются, поэтому многократный вызов метода создаст множество одинаковых объектов. Если вы хотите создать полностью новый объект, необходимо по-новой инициировать конструктор.
- `ДанныеСтроки` - Возвращает данные текущей строки табличной части объекта.
- `ДанныеОбъекта` - Возвращает данные создаваемого объекта.
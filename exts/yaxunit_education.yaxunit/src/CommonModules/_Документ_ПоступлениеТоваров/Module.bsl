
#Область СлужебныйПрограммныйИнтерфейс
Процедура ИсполняемыеСценарии() Экспорт

	ЮТТесты.ВТранзакции().УдалениеТестовыхДанных()
		.ДобавитьСерверныйТест("ЗаполнениеРеквизитовПоУмолчанию")
		.ДобавитьСерверныйТест("ПроверкаСозданияДокумента")
	;

КонецПроцедуры

Процедура ЗаполнениеРеквизитовПоУмолчанию() Экспорт
	
	Организация = ЮТест.Данные().СоздатьЭлемент(Справочники.Организации);
	Склад = ЮТест.Данные().СоздатьЭлемент(Справочники.Склады);

	ДокументОбъект = Документы.ПоступлениеТоваров.СоздатьДокумент();
	ДокументОбъект.Заполнить(Неопределено);
	
	ЮТест.ОжидаетЧто(ДокументОбъект)
		.Свойство("Организация")
		.Заполнено()
		.Равно(Справочники.Организации.ОсновнаяОрганизация)
		.Свойство("Склад")
		.Равно(Справочники.Склады.ОсновнойСклад);
	
	ДокументОбъект = Документы.ПоступлениеТоваров.СоздатьДокумент();
	ЗначенияЗаполнения = Новый Структура("Организация, Склад", Организация, Склад);
	ДанныеЗаполнения = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ДокументОбъект.Заполнить(ДанныеЗаполнения);
	
	ПредикатОрганизацияСклад = ЮТест.Предикат()
		.Реквизит("Организация").Равно(Организация)
		.Реквизит("Склад").Равно(Склад)
		.Получить();
	
	ЮТест.ОжидаетЧто(ДокументОбъект)
		.СоответствуетПредикату(ПредикатОрганизацияСклад);
	
КонецПроцедуры

Процедура ПроверкаСозданияДокумента() Экспорт
	
	#Если Сервер Тогда
		Конструктор = ЮТест.Данные().КонструкторОбъекта(Документы.ПоступлениеТоваров);
	#Иначе
		Конструктор = ЮТест.Данные().КонструкторОбъекта("Документы.ПоступлениеТоваров");
	#КонецЕсли
	
	Конструктор
		.ФикцияОбязательныхПолей()
		.Фикция("Склад")
		.Установить("Контрагент", ЮТест.Данные().СоздатьЭлемент("Справочники.Контрагенты"))
		.ТабличнаяЧасть("Товары");

	Для Инд1 = 1 По ЮТест.Данные().СлучайноеЧисло(1, 5) Цикл
		Конструктор.ДобавитьСтроку()
			.Фикция("Номенклатура")
			.Установить("Количество", ЮТест.Данные().СлучайноеПоложительноеЧисло(20))
			.Установить("Цена", ЮТест.Данные().СлучайноеПоложительноеЧисло(9999, 2))
			.Установить("Сумма", Конструктор.ДанныеСтроки().Цена * Конструктор.ДанныеСтроки().Количество);
	КонецЦикла;

	Ссылка = Конструктор.Провести();

КонецПроцедуры
#КонецОбласти
